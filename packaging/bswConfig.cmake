cmake_minimum_required(VERSION 3.19)

set(bsw_known_comps static shared)
set(bsw_comp_static NO)
set(bsw_comp_shared NO)
foreach (bsw_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (bsw_comp IN_LIST bsw_known_comps)
        set(bsw_comp_${bsw_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "bsw does not recognize component `${bsw_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (bsw_comp_static AND bsw_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "bsw `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(bsw_static_targets "${CMAKE_CURRENT_LIST_DIR}/bsw-static-targets.cmake")
set(bsw_shared_targets "${CMAKE_CURRENT_LIST_DIR}/bsw-shared-targets.cmake")

macro(bsw_load_targets type)
    if (NOT EXISTS "${bsw_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "bsw `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${bsw_${type}_targets}")
endmacro()

if (bsw_comp_static)
    bsw_load_targets(static)
elseif (bsw_comp_shared)
    bsw_load_targets(shared)
elseif (DEFINED bsw_SHARED_LIBS AND bsw_SHARED_LIBS)
    bsw_load_targets(shared)
elseif (DEFINED bsw_SHARED_LIBS AND NOT bsw_SHARED_LIBS)
    bsw_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${bsw_shared_targets}")
        bsw_load_targets(shared)
    else ()
        bsw_load_targets(static)
    endif ()
else ()
    if (EXISTS "${bsw_static_targets}")
        bsw_load_targets(static)
    else ()
        bsw_load_targets(shared)
    endif ()
endif ()